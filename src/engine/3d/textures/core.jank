(ns engine.3d.textures.core
  (:require [engine.io.interface :as io]
            [engine.macros :refer [clet]]
            [engine.gl.constants :as gl]))

(cpp/raw "#include <GLFW/glfw3.h>")

;; STB Image implementation is in libs/stb/lib/libstb_image.a for AOT compilation
(cpp/raw
 "#define STBI_NO_THREAD_LOCALS 1
  #include \"stb_image.h\"")

(cpp/raw
 "extern \"C\" {
   void glGenerateMipmap(GLenum target);
 }")

(cpp/raw
 "inline void* voidify_stbi_uc (stbi_uc*  x) {
    return (void*) x;
  }")

;; Using centralized GL constants from engine.gl.constants


(defn load-texture
  [{:keys [path alpha-channel?
           wrap-s wrap-t
           min-filter mag-filter
           flip-vertically?]
    :or {wrap-s gl/GL_REPEAT
         wrap-t gl/GL_REPEAT
         min-filter gl/GL_LINEAR
         mag-filter gl/GL_LINEAR
         flip-vertically? false}
    :as args}]
  (clet [width (cpp/int)
         height (cpp/int)
         channel-count (cpp/int)
         _ (cpp/stbi_set_flip_vertically_on_load (if flip-vertically? 1 0))
         data (cpp/stbi_load path
                             (cpp/& width)
                             (cpp/& height)
                             (cpp/& channel-count)
                             0)
         :when (cpp/! data)
         :error (throw (ex-info "Failed to load texture image"
                                args))
         texture ((cpp/type "unsigned int"))
         _ (cpp/glGenTextures 1 (cpp/& texture))
         _ (cpp/glBindTexture gl/GL_TEXTURE_2D texture)
         _ (cpp/glTexParameteri gl/GL_TEXTURE_2D gl/GL_TEXTURE_WRAP_S wrap-s)
         _ (cpp/glTexParameteri gl/GL_TEXTURE_2D gl/GL_TEXTURE_WRAP_T wrap-t)
         _ (cpp/glTexParameteri gl/GL_TEXTURE_2D gl/GL_TEXTURE_MIN_FILTER min-filter)
         _ (cpp/glTexParameteri gl/GL_TEXTURE_2D gl/GL_TEXTURE_MAG_FILTER mag-filter)
         _ (cpp/glTexImage2D
            gl/GL_TEXTURE_2D
            0
            (if alpha-channel? gl/GL_RGBA gl/GL_RGB)
            width
            height
            0
            (if alpha-channel? gl/GL_RGBA gl/GL_RGB)
            gl/GL_UNSIGNED_BYTE
            (cpp/voidify_stbi_uc data))
         _ (cpp/glGenerateMipmap gl/GL_TEXTURE_2D)
         _ (cpp/stbi_image_free (cpp/voidify_stbi_uc data))]

        texture))
